plugins {
    id 'com.android.application' version '3.5.0-beta05'
    id 'com.google.android.gms.oss-licenses-plugin' version '0.9.5'
    id 'com.diffplug.gradle.spotless' version '3.23.1'
    id 'io.gitlab.arturbosch.detekt' version '1.0.0-RC15'
    id 'org.jetbrains.kotlin.android' version '1.3.40'
    id 'org.jetbrains.kotlin.android.extensions' version '1.3.40'
    id 'org.jetbrains.kotlin.kapt' version '1.3.40'
    id 'androidx.navigation.safeargs.kotlin' version '2.1.0-alpha05'
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.github.dnaka91.beatfly"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'en'
    }
    signingConfigs {
        release {
            def props = new Properties()
            props.load(rootProject.file('local.properties').newInputStream())

            storeFile = rootProject.file('keystore.jks')
            storePassword = props.get('keystore.password')
            keyAlias = props.get('keystore.key-alias')
            keyPassword = props.get('keystore.key-password')
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}

detekt {
    config = rootProject.files('.detekt.yml')
}

kapt {
    arguments {
        arg 'dagger.fastInit', 'enabled'
        arg 'dagger.fullBindingGraphValidation', 'ERROR'
        arg 'dagger.gradle.incremental'
    }
}

spotless {
    kotlin {
        target 'src/**/*.kt'
        licenseHeaderFile rootProject.file('license.kt')
        trimTrailingWhitespace()
        endWithNewline()
    }
}

dependencies {
    implementation 'org.jetbrains.anko:anko-support-v4-commons:0.10.8'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.40"
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'
    implementation 'androidx.core:core-ktx:1.1.0-rc01'
    implementation 'androidx.fragment:fragment-ktx:1.1.0-beta01'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-beta01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0-beta01'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.1.0-beta01'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.1.0-alpha05'
    implementation 'androidx.navigation:navigation-ui-ktx:2.1.0-alpha05'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.10.1'
    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.2'
    implementation 'com.google.android.material:material:1.1.0-alpha07'
    implementation 'com.google.dagger:dagger-android:2.23.1'
    implementation 'com.google.dagger:dagger-android-support:2.23.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
    implementation 'com.squareup.moshi:moshi:1.8.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    kapt 'com.google.dagger:dagger-compiler:2.23.1'
    kapt 'com.google.dagger:dagger-android-processor:2.23.1'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.8.0'
}
