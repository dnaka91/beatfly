plugins {
    id 'com.android.application' version '4.1.1'
    id 'com.google.android.gms.oss-licenses-plugin' version '0.10.2'
    id 'com.diffplug.spotless' version '5.7.0'
    id 'io.gitlab.arturbosch.detekt' version '1.14.2'
    id 'org.jetbrains.kotlin.android' version '1.4.21'
    id 'org.jetbrains.kotlin.kapt' version '1.4.21'
    id 'androidx.navigation.safeargs.kotlin' version '2.3.2'
    id 'dagger.hilt.android.plugin' version '2.30.1-alpha'
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.github.dnaka91.beatfly"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'en'
    }
    signingConfigs {
        release {
            def props = new Properties()
            props.load(rootProject.file('local.properties').newInputStream())

            storeFile = rootProject.file('keystore.jks')
            storePassword = props.get('keystore.password')
            keyAlias = props.get('keystore.key-alias')
            keyPassword = props.get('keystore.key-password')
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

detekt {
    config = rootProject.files('.detekt.yml')
}

kapt {
    arguments {
        arg 'dagger.fastInit', 'enabled'
        arg 'dagger.fullBindingGraphValidation', 'ERROR'
    }
}

spotless {
    kotlin {
        target 'src/**/*.kt'
        licenseHeaderFile rootProject.file('license.kt')
        trimTrailingWhitespace()
        endWithNewline()
    }
    format 'xml', {
        target 'src/**/*.xml'
        licenseHeaderFile rootProject.file('license.xml'), '<[a-zA-Z]'
        trimTrailingWhitespace()
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.4.21'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.media:media:1.2.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.2'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.12.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.dagger:hilt-android:2.30.1-alpha'
    implementation 'com.louiscad.splitties:splitties-dimensions:2.1.1'
    implementation 'com.louiscad.splitties:splitties-intents:2.1.1'
    implementation 'com.louiscad.splitties:splitties-preferences:2.1.1'
    implementation 'com.squareup.moshi:moshi:1.11.0'
    implementation 'io.coil-kt:coil:1.1.0'

    testImplementation 'junit:junit:4.13.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha02'
    kapt 'com.google.dagger:hilt-compiler:2.30.1-alpha'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.11.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'
}
